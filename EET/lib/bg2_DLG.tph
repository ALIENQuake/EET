/////                                                  \\\\\
///// IMOEN DV                                         \\\\\
/////                                                  \\\\\

COPY_EXISTING ~AATAQAH.DLG~ ~override~
	~IGOLEM2.DLG~ ~override~
	~ISHADMT1.DLG~ ~override~
	~JAHEIRA.DLG~ ~override~
	~MINSCA.DLG~ ~override~
	~RIELEV.DLG~ ~override~
	~UNUSED.DLG~ ~override~ //well, unused but still...
	~YOSHIMO.DLG~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~"IMOEN"~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~"IMOEN2"~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

/////                                                  \\\\\
///// FATESP.DLG                                       \\\\\
/////                                                  \\\\\

<<<<<<<< .../FATESP.d
REPLACE FATESP
IF ~  Global("TalkedToFatesp","GLOBAL",1)
~ THEN BEGIN 6
  SAY #65009 /* ~Is there a thread you wish me to search for, child of Bhaal? I will bring them to this place, if I can.~ */
  IF ~~ THEN REPLY #65029 /* ~Never mind. I can't think of anyone else.~ */ DO ~SetGlobalTimer("FateSpiritTimer","AR4500",1)
~ GOTO 9
END
END
>>>>>>>>

COMPILE ~.../FATESP.d~

/////                                                  \\\\\
///// IMOEN10.DLG                                      \\\\\
/////                                                  \\\\\

//check if Minsc and Jaheira are really in the AR0602
COPY_EXISTING ~IMOEN10.DLG~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~~~~~\(IF ~\)\(~ THEN REPLY #61011 GOTO 27\)~~~~~ ~\1InMyArea("Jaheira") InMyArea("Minsc")\2~
	END
BUT_ONLY

/////                                                  \\\\\
///// TOWNC01.DLG                                      \\\\\
/////                                                  \\\\\

COPY_EXISTING ~TOWNC01.DLG~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~CalanderDayLT(~ ~GlobalLT("K#TOWNC01_cnt","GLOBAL",~
	END
BUT_ONLY
